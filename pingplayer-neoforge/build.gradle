plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.78'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name + "-neoforge"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        server {
            server()
            programArgument '--nogui'
        }
    }

    mods {
        "${project.mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.named('processResources', ProcessResources).configure {
    def replaceProperties = [
            mod_id                : project.mod_id,
            mod_name              : project.mod_name,
            mod_license           : project.mod_license,
            mod_version           : project.mod_version,
            mod_authors           : project.mod_authors,
            mod_description       : project.mod_description,
            minecraft_version_range: project.minecraft_version_range,
            neo_version_range     : project.neo_version_range,
            loader_version_range  : project.loader_version_range
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'    : project.mod_id,
                'Specification-Version'  : project.mod_version,
                'Implementation-Title'   : project.mod_name,
                'Implementation-Version' : project.mod_version,
                'Implementation-Vendor'  : project.mod_authors
        ])
    }

    from(rootProject.file("LICENSE")) {
        rename { "${it}_player-ping-neoforge" }
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        // Add repositories here if needed
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
